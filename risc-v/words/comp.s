# SPDX-License-Identifier: GPL-3.0-only
# Comparision operators

# -----------------------------------------------------------------------------
  CODEWORD "0=",ZEROEQUAL # ( n -- f ) COMP: f true if n is zero
# ( x -- ? )
# -----------------------------------------------------------------------------
  sltiu s3, s3, 1
  addi s3, s3, -1
  xori s3, s3, -1
  NEXT

# -----------------------------------------------------------------------------
  CODEWORD  "0<>", NOTZEROEQUAL # ( n -- f ) COMP: f true if n is not zero 
# -----------------------------------------------------------------------------
  sltiu s3, s3, 1
  addi s3, s3, -1
  NEXT

# -----------------------------------------------------------------------------
  CODEWORD  "0<", ZEROLESS # ( n -- f ) COMP: f true if n is negative
# -----------------------------------------------------------------------------
  srai s3, s3, 31
  NEXT

# -----------------------------------------------------------------------------
  CODEWORD  ">=", GREATEREQUAL # ( n2 n1 -- f ) COMP: f true if n2 >= n1 
# -----------------------------------------------------------------------------
  lw t0, 0(s4)
  addi s4, s4, 4
  slt s3, t0, s3
  addi s3, s3, -1
  NEXT

# -----------------------------------------------------------------------------
  CODEWORD  "<=", LESSEQUAL # ( n2 n1 -- f ) COMP: f true if n2 <= n1          
# -----------------------------------------------------------------------------
  lw t0, 0(s4)
  addi s4, s4, 4
  slt s3, s3, t0
  addi s3, s3, -1
  NEXT

# -----------------------------------------------------------------------------
  CODEWORD  "<", LESS # ( n2 n1 -- f ) COMP: f true if n2 < n1 
                      # Checks if s5 is less than ra.
# -----------------------------------------------------------------------------
  lw t0, 0(s4)
  addi s4, s4, 4
  slt s3, t0, s3
  addi s3, s3, -1
  xori s3, s3, -1
  NEXT

# -----------------------------------------------------------------------------
  CODEWORD  ">", GREATER # ( n2 n1 -- f ) COMP: f true if n2 > n1 
                      # Checks if s5 is greater than ra.
# -----------------------------------------------------------------------------
  lw t0, 0(s4)
  addi s4, s4, 4
  slt s3, s3, t0
  addi s3, s3, -1
  xori s3, s3, -1
  NEXT

# -----------------------------------------------------------------------------
  CODEWORD  "u>=", UGREATEREQUAL # ( u2 u1 -- f ) COMP: f true if unsigned u2 >= u1
# -----------------------------------------------------------------------------
  lw t0, 0(s4)
  addi s4, s4, 4
  sltu s3, t0, s3
  addi s3, s3, -1
  NEXT

# -----------------------------------------------------------------------------
  CODEWORD  "u<=", ULESSEQUAL # ( u2 u1 -- f ) COMP: f true if unsigned u2 <= u1
# -----------------------------------------------------------------------------
  lw t0, 0(s4)
  addi s4, s4, 4
  sltu s3, s3, t0
  addi s3, s3, -1
  NEXT

# -----------------------------------------------------------------------------
  CODEWORD  "u<", ULESS # ( u2 u1 -- f ) COMP: f true if unsigned u2 < u1 
# -----------------------------------------------------------------------------
  lw t0, 0(s4)
  addi s4, s4, 4
  sltu s3, t0, s3
  addi s3, s3, -1
  xori s3, s3, -1
  NEXT

# -----------------------------------------------------------------------------
  CODEWORD  "u>", UGREATER # ( u2 u1 -- f ) COMP: f true if unsigned u2 > u1 
# -----------------------------------------------------------------------------
  lw t0, 0(s4)
  addi s4, s4, 4
  sltu s3, s3, t0
  addi s3, s3, -1
  xori s3, s3, -1
  NEXT

# -----------------------------------------------------------------------------
  CODEWORD  "<>", NOTEQUAL # ( n2 n1 -- f ) COMP: f true if n2 not equal n1 

                       # Compares the top two stack elements for inequality.
# -----------------------------------------------------------------------------
  lw t0, 0(s4)
  addi s4, s4, 4
  xor s3, s3, t0
  sltiu s3, s3, 1
  addi s3, s3, -1
  NEXT

# -----------------------------------------------------------------------------
  CODEWORD  "=", EQUAL # ( n2 n1 -- f ) COMP: f true if n2 equal n1 
                      # Compares the top two stack elements for equality.
# -----------------------------------------------------------------------------
equal_einsprung:
  lw t0, 0(s4)
  addi s4, s4, 4
  xor s3, s3, t0
  sltiu s3, s3, 1
  addi s3, s3, -1
  xori s3, s3, -1
  NEXT

# -----------------------------------------------------------------------------
  CODEWORD  "min", MIN # ( n2 n1 -- n ) COMP: n is lesser of n2 and n1 
                        # s3 is the lesser of ra and s5.
# -----------------------------------------------------------------------------
  lw t0, 0(s4)
  addi s4, s4, 4
  blt s3, t0, 1f
    mv s3, t0
1:NEXT

# -----------------------------------------------------------------------------
  CODEWORD  "max", MAX # ( n2 n1 -- n ) COMP: n is greater of n2 and n1 
                        # s3 is the greater of ra and s5.
# -----------------------------------------------------------------------------
  lw t0, 0(s4)
  addi s4, s4, 4
  blt t0, s3, 1f
    mv s3, t0
1:NEXT

# -----------------------------------------------------------------------------
  CODEWORD  "umin", UMIN # ( u2 u1 -- u ) COMP: u is unsigned lesser of u2 and u1 
# -----------------------------------------------------------------------------
  lw t0, 0(s4)
  addi s4, s4, 4
  bltu s3, t0, 1f
    mv s3, t0
1:NEXT

# -----------------------------------------------------------------------------
  CODEWORD  "umax", UMAX # ( u2 u1 -- u ) COMP: u is unsigned greater of u2 and u1 
# -----------------------------------------------------------------------------
  lw t0, 0(s4)
  addi s4, s4, 4
  bltu t0, s3, 1f
    mv s3, t0
1:NEXT
